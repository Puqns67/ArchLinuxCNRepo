# Maintainer: Integral <integral@member.fsf.org>
# Contributor: Evgenii Alekseev
# Contributor: Felix Yan <felixonmars@archlinux.org>
# Contributor: Manuel Schneider  <manuelschneid3r at googles mail>

pkgname=albert
pkgver=33.0.0
pkgrel=1
pkgdesc="A sophisticated standalone keyboard launcher"
arch=('x86_64')
url="https://${pkgname}launcher.github.io"
license=('LicenseRef-Albert')
depends=(
	'libqalculate'
	'python'
	'python-pip'
	'qt6-base'
	'qt6-declarative'
	'qt6-5compat'
	'qt6-scxml'
	'qt6-shadertools'
	'qt6-svg'
	'qtkeychain-qt6'
)
makedepends=('cmake' 'qt6-tools')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/${pkgname}launcher/${pkgname}/releases/download/v${pkgver}/v${pkgver}.tar.gz")
sha512sums=('e5b2a6950e71aaa7bbaa5e13fd5e8f4ef366428f5b24fd85c265a50570cd272f5fd00634486f08798e12f49cd149a12d71cfc12070db833626b1303891ecb293')

build() {
	cmake -B build \
		-S "${pkgname}" \
		-DCMAKE_BUILD_TYPE=None \
		-DCMAKE_INSTALL_PREFIX=/usr \
		-DCMAKE_INSTALL_LIBDIR=lib \
		-DCMAKE_FIND_PACKAGE_RESOLVE_SYMLINKS=ON \
		-DQHOTKEY_INSTALL=OFF

	cmake --build build
}

package() {
	DESTDIR="${pkgdir}" cmake --install build
	install -Dm644 "${pkgname}/LICENSE.md" -t "${pkgdir}/usr/share/licenses/${pkgname}/"
}
