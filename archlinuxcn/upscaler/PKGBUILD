# Maintainer: Fabio 'Lolix' Loli <fabio.loli@disroot.org> -> https://github.com/FabioLolix
# Contributor: Igor Dyatlov <dyatlov.igor@protonmail.com>

pkgname=upscaler
pkgver=1.6.2
pkgrel=1
pkgdesc="Upscale and enhance images"
arch=('x86_64')
url="https://gitlab.gnome.org/World/Upscaler"
license=(GPL-3.0-only)
depends=('libadwaita>=1.8.0'
         python-gobject
         python-pillow
         python-vulkan
         upscayl-ncnn
         python
         gtk4
         pango
         glib2)
makedepends=(git
             blueprint-compiler
             meson)
checkdepends=(appstream-glib)
source=("git+https://gitlab.gnome.org/World/Upscaler.git#tag=${pkgver}")
b2sums=('28a35ec4e7cb66e0aca414f55eb767dbdb5ce7ea2eab238c5fdc70273af97483f5258e03b7c0b3232f4936cc6e12b1614a0756e8efc699fc6b7386fd7fa5ef19')

build() {
  arch-meson Upscaler build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs || :
}

package() {
  meson install -C build --destdir "$pkgdir"
}
