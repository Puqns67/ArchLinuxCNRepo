pkgname=plots
_name=Plots

pkgver=0.8.5
pkgrel=4
pkgdesc="A graph plotting app for GNOME"
arch=('any')
url="https://github.com/alexhuntley/Plots/"
license=('GPL')
depends=(
	'gtk4'
	'libhandy'
	'python-gobject'
	'python-opengl'
	'python-jinja'
	'python-numpy'
	'python-lark-parser'
	'otf-latinmodern-math'
	'python-freetype-py'
	'python-pyglm'
)

makedepends=('python-setuptools' git desktop-file-utils)
checkdepends=('python-pytest')

options=(!lto)

source=(
	"source::git+https://github.com/alexhuntley/$_name#tag=v$pkgver"
)
sha256sums=('43399270917e599f5beb67940a6661d8875f2afb65a44ebbdc7e6d7fc5a25d36')

build() {
	cd source
	python setup.py build
}

package() {
	cd source
	rm -rf build/lib/tests
	python setup.py install --root="$pkgdir" --optimize=1 --skip-build
	install -D -t "$pkgdir/usr/share/help/C/plots/" help/C/*
	install -Dm644 res/com.github.alexhuntley.Plots.svg "$pkgdir/usr/share/icons/hicolor/scalable/apps/plots.svg"
	install -Dm644 res/com.github.alexhuntley.Plots-symbolic.svg "$pkgdir/usr/share/icons/hicolor/symbolic/apps/plots-symbolic.svg"
	install -Dm644 res/com.github.alexhuntley.Plots.desktop "$pkgdir/usr/share/applications/plots.desktop"
	sed -Ei 's/^(Icon=).*$/\1plots/' "$pkgdir/usr/share/applications/plots.desktop"
	desktop-file-edit \
		--set-key=Exec \
		--set-value='env GDK_DEBUG=gl-prefer-gl plots' \
		"$pkgdir/usr/share/applications/plots.desktop"
	mv \
		"$pkgdir/usr/share/applications/plots.desktop" \
		"$pkgdir/usr/share/applications/com.github.alexhuntley.Plots.desktop"
}
