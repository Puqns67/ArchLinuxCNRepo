_name=ormsgpack
pkgname=python-${_name}
pkgver=1.11.0
pkgrel=1
pkgdesc="Fast, correct Python msgpack library supporting dataclasses, datetimes, and numpy."
arch=('any')
url='https://github.com/aviramha/ormsgpack'
license=('Apache-2.0 OR MIT')
source=("ormsgpack::git+${url}#tag=${pkgver}")
sha256sums=('607c2f7c78ed758d09c8e27de55a3438ec71d0d02b27e462654813d6bf46028b')
depends=('python' 'gcc-libs')
makedepends=('python-maturin' 'python-build' 'python-installer' 'python-wheel' 'git')
checkdepends=('python-msgpack' 'python-numpy' 'python-pendulum' 'python-pydantic' 'python-pytest' 'python-pytest-benchmark' 'python-dateutil' 'python-pytz' 'python-tzdata')
optdepends=('python-aiohttp: aiohttp')

build() {
  cd "${srcdir}"/ormsgpack
  python -m build --wheel --no-isolation
}

check() {
  local pytest_options=(
    -vv

  )
  cd "${srcdir}"/ormsgpack
  python -m venv --system-site-packages test-env
  test-env/bin/python -m installer dist/*.whl
  test-env/bin/python -m pytest "${pytest_options[@]}" tests
}

package() {
  cd "${srcdir}"/ormsgpack
  python -m installer --destdir="$pkgdir" dist/*.whl
}
