pkgname=julia-git-videoio-src
pkgver=1.3.0
_commit=6722199244795f69e158b1d88c9b97a80a55cfa7
pkgrel=2
pkgdesc="VideoIO.jl"
url="https://github.com/JuliaIO/VideoIO.jl.git"
arch=('any')
license=('MIT')
makedepends=(git julia-pkg-scripts)
depends=(julia-git)
source=("git+https://github.com/JuliaIO/VideoIO.jl.git#commit=$_commit"
        0001-Remove-deprecated-ticks_per_frame.patch
        0002-Support-both-FFMPEG-7-and-8.patch)
sha256sums=('0d4792ba160b0cb4170e510698e3fb5afd4d1fee7e2df91e6c0642da0d69a4dd'
            '0009d92fa8cdba88c7138468f240c5b91956775d421a39b4e0aab063312c6133'
            'ee4b8532840ddabaa4c1f94cb5a995b9f9006fc61287550b1040e194ee9c0075')

prepare() {
  cd VideoIO.jl

  patch -Np1 --no-backup-if-mismatch < "../0001-Remove-deprecated-ticks_per_frame.patch"
  patch -Np1 --no-backup-if-mismatch < "../0002-Support-both-FFMPEG-7-and-8.patch"
}

package() {
  cd VideoIO.jl

  JULIA_INSTALL_SRCPKG=1 . /usr/lib/julia/julia-install-pkg.sh VideoIO "${pkgdir}" "${pkgname}" julia-git
}
