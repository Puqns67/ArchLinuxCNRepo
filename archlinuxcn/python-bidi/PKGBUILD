# Maintainer: Hu Butui <hot123tea123@gmail.com>
# Contributor: tinywrkb <tinywrkb@gmail.com>
# Contributor: Johannes Titz <johannes.titz@gmail.com>
# Contributor: Doron Behar <doron.behar@gmail.com>

_pkgname=python-bidi
pkgname=python-bidi
pkgver=0.6.7
pkgrel=1
pkgdesc='BIDI algorithm related functions'
url='https://github.com/MeirKriheli/python-bidi'
license=(GPL-3.0-or-later)
arch=(x86_64)
depends=(
  python
)
makedepends=(
  python-build
  python-installer
  python-maturin
  python-setuptools
  python-wheel
)

source=("${_pkgname}-${pkgver}.tar.gz::https://github.com/MeirKriheli/python-bidi/archive/refs/tags/v${pkgver}.tar.gz")
sha512sums=('745de39d53084c484dae6485ebb23d4a5f6630af8b2e0c7e7e7ec73eeec47d7974553021a073a970ebd4c76d4f183caaac79948d8dd6ebb10d87070c0ea5832c')

build() {
  cd ${_pkgname}-${pkgver}
  python -m build --wheel --no-isolation
}

package() {
  cd ${_pkgname}-${pkgver}
  python -m installer --destdir="${pkgdir}" dist/*.whl
}
# vim:set ts=2 sw=2 et:
