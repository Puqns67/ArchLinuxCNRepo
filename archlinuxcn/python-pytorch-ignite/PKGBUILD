# Maintainer: Butui Hu <hot123tea123@gmail.com>

pkgname=python-pytorch-ignite
_pkgname=ignite
pkgver=0.5.2
pkgrel=1
pkgdesc='High-level library to help with training and evaluating neural networks in PyTorch flexibly and transparently'
arch=('any')
url='https://github.com/pytorch/ignite'
license=('BSD-3-Clause')
depends=(
  python-pytorch
)
makedepends=(
  python-build
  python-hatchling
  python-installer
  python-wheel
)

source=("${pkgname}-${pkgver}.tar.gz::https://github.com/pytorch/ignite/archive/refs/tags/v${pkgver}.tar.gz")
sha512sums=('371be1ea57d9f71e5c9e5444a0cb60856cebcb64d9f846ac1c96f4f0296cdc88fff630d40bdeeca63324186e3fea88c8acd742e224404ca55de65e577aea5124')

build() {
  cd "${_pkgname}-${pkgver}"
  python -m build --wheel --no-isolation
}

package() {
  cd "${_pkgname}-${pkgver}"
  python -m installer --destdir="${pkgdir}" dist/*.whl
  install -Dm644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
# vim:set ts=2 sw=2 et:
