# Maintainer: Butui Hu <hot123tea123@gmail.com>
# Contributor: peippo <christoph.fink@gmail.com>

pkgname=python-av
_pkgname=PyAV
pkgdesc="Pythonic bindings for FFmpeg's libraries"
url='https://pyav.org'
pkgver=16.0.1
pkgrel=1
arch=('x86_64')
license=('BSD-3-Clause')
makedepends=(
  cython
  python-build
  python-installer
  python-pip
  python-setuptools
  python-wheel
)
depends=(
  ffmpeg
  glibc
  python-numpy
  python-pillow
)
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/PyAV-Org/PyAV/archive/v${pkgver}.tar.gz")
sha512sums=('20001ae3250213dc9d6a3a4d275fd9d1db312cb108534e32e2833347cb2b6e8d54be93cfb271bdbbe6d44404bb197893a1411f0cd31bf9b6764330c20fbc50ea')

build() {
  cd "${_pkgname}-${pkgver}"
  GITHUB_ACTIONS=true \
  python -m build --wheel --no-isolation --skip-dependency-check
}

package() {
  cd "${_pkgname}-${pkgver}"
  python -m installer --destdir="${pkgdir}" dist/*.whl
  install -Dm644 LICENSE.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
# vim:set ts=2 sw=2 et:
