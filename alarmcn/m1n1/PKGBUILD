# Maintainer: Hector Martin <marcan@marcan.st>

pkgname=m1n1
pkgver=1.5.2
pkgrel=1
pkgdesc='Asahi Linux bootloader'
arch=('aarch64')
url='http://asahilinux.org'
license=('MIT')
makedepends=(git imagemagick)
source=(
  "git+https://github.com/AsahiLinux/m1n1#tag=v${pkgver}"
)
sha256sums=('d8317d3f0d1993d355790a19eb5b1b92f335e2b4f3d8f77a6ef7e70642b802c5')
b2sums=('a56c0cc301dfc2d815662756e4da2c590bd9c24972289f74c4cfa29104ac6b1d789322d377890fc3f8a305d69507cef0a863be875a80152b220f8f977a3759b2')

prepare() {
  cd m1n1
  git submodule update --init --recursive
}

build() {
  cd m1n1
  make ARCH= RELEASE=1 -j1
}

package() {
  install -Dt "$pkgdir/usr/lib/asahi-boot" -m644 "m1n1/build/m1n1.bin"
  install -Dm644 m1n1/LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
