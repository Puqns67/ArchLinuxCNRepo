# Maintainer: Hector Martin <marcan@marcan.st>

pkgname=m1n1
pkgver=1.5.1
pkgrel=1
pkgdesc='Asahi Linux bootloader'
arch=('aarch64')
url='http://asahilinux.org'
license=('MIT')
makedepends=(git imagemagick)
source=(
  "git+https://github.com/AsahiLinux/m1n1#tag=v${pkgver}"
)
sha256sums=('5cfba96d43f055a60f4996bde0c7c289dc9bd475aeac0ba5afc8b8fcf482ce92')
b2sums=('4abcc352f6ed3e927c9b1db83baa137cc1aa87d51c8bd3f86a74e2659482509edfc53fa4e8e4b5ee9a5320e1dade6b5797fc6d91115dc9952fd39ae1f2b346e5')

prepare() {
  cd m1n1
  git submodule update --init --recursive
}

build() {
  cd m1n1
  make ARCH= RELEASE=1 -j1
}

package() {
  install -Dt "$pkgdir/usr/lib/asahi-boot" -m644 "m1n1/build/m1n1.bin"
  install -Dm644 m1n1/LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
