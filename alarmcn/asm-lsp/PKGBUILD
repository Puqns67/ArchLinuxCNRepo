# Maintainer: Integral <integral@member.fsf.org>
# Contributor: Kino <cybao292261@163.com>
# Contributor: Mattia Ronchi <ronchimattia03 at gmail dot com>

pkgname=asm-lsp
pkgver=0.10.1
pkgrel=1
pkgdesc="A language server for NASM/GAS/GO Assembly"
arch=('x86_64' 'aarch64' 'riscv64')
url="https://github.com/bergercookie/${pkgname}"
license=('BSD-2-Clause')
depends=('openssl')
makedepends=('cargo')
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/refs/tags/v${pkgver}.tar.gz")
sha256sums=('9500dd7234966ae9fa57d8759edf1d165acd06c4924d7dbeddb7d52eb0ce59d6')
options=('!lto')

prepare() {
	cd "${pkgname}-${pkgver}/"
	cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
	cd "${pkgname}-${pkgver}/"
	cargo build --frozen --release --all-features
}

check() {
	cd "${pkgname}-${pkgver}/"
	cargo test --frozen --all-features
}

package() {
	cd "${pkgname}-${pkgver}/"
	install -Dm755 "target/release/${pkgname}" -t "$pkgdir/usr/bin/"
	install -Dm644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}/"
}
